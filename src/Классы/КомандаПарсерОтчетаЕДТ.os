Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Аргумент("PATH", "", "Файл с отчетом")
			.ТСтрока();

	Команда.Аргумент("WORKDIR", "", "Корень репозитория или проектной области")
			.ТСтрока();
	
	Команда.Аргумент("PROJECTS", Новый Массив, "Список проектов, которые были переданы в ЕДТ для анализа")
				.ТМассивСтрок();
	
	Команда.Аргумент("OUT", "", "Путь к файлу отчета в формате BSL LS")
	.ТСтрока();

	Команда.Опция("nixPath", Истина, "Использовать пути как в линукс системах")
	.ТБулево();

КонецПроцедуры

Процедура ПередВыполнениемКоманды(Знач Команда) Экспорт
	
	Лог = ПараметрыПриложения.Лог();
	
	БылиОшибки = Ложь;

	ФайлДляОбработки = Новый Файл(Команда.ЗначениеАргумента("PATH"));
	
	Если НЕ (ФайлДляОбработки.Существует() И ФайлДляОбработки.ЭтоФайл()) Тогда
		Лог.Ошибка("Не найден файл %1", Команда.ЗначениеАргумента("PATH"));
		БылиОшибки = Истина;
	КонецЕсли;
	
	Репозиторий = Новый Файл(Команда.ЗначениеАргумента("WORKDIR"));
	
	Если НЕ (Репозиторий.Существует() И Репозиторий.ЭтоКаталог()) Тогда
		Лог.Ошибка("Не найден корневой каталог проектов %1", Команда.ЗначениеАргумента("WORKDIR"));
		БылиОшибки = Истина;
	КонецЕсли;

	Проекты = Команда.ЗначениеАргумента("PROJECTS");

	Для Каждого Проект Из Проекты Цикл
		
		ДиректорияПроекта = Новый Файл(ОбъединитьПути(Команда.ЗначениеАргумента("WORKDIR"), Проект));
	
		Если НЕ (ДиректорияПроекта.Существует() И ДиректорияПроекта.ЭтоКаталог()) Тогда
			Лог.Ошибка("Не найден корневой каталог проекта %1 в %2", Проект, Команда.ЗначениеАргумента("WORKDIR"));
			БылиОшибки = Истина;
		КонецЕсли;
	
	КонецЦикла;

	Если БылиОшибки Тогда
		ВызватьИсключение "Продолжение работы невозможно";
	КонецЕсли;

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	
	ФайлДляОбработки	= Команда.ЗначениеАргумента("PATH");
	Репозиторий			= Команда.ЗначениеАргумента("WORKDIR");
	Проекты				= Команда.ЗначениеАргумента("PROJECTS");
	ФайлРезультата		= Команда.ЗначениеАргумента("OUT");
	ПутиКакВЛинукс		= Команда.ЗначениеОпции("nixPath");

	ПарсерОшибок.Прочитать(ФайлДляОбработки, Репозиторий, Проекты, ФайлРезультата, ПутиКакВЛинукс);

КонецПроцедуры
