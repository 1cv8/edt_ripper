#Использовать fs

#Область ОписаниеПеременных

Перем ИсключенияПоID; // Соответствие - ID EDT, игнорируемые обработкой
Перем ИсключенияПоКоду; // Соответствие - Коды правил, игнорируемые обработкой

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция ЕстьФильтрПравилПоID() Экспорт
	Возврат (ИсключенияПоID <> Неопределено И ИсключенияПоID.Количество() > 0);
КонецФункции

Функция ЕстьФильтрПравилПоКоду() Экспорт
	Возврат (ИсключенияПоКоду <> Неопределено И ИсключенияПоКоду.Количество() > 0);
КонецФункции

Функция ИгнорироватьПравилоПоID(ID) Экспорт
	
	Возврат (ИсключенияПоID.Получить(ID) = Истина);

КонецФункции

Функция ИгнорироватьПравилоПоКоду(ID) Экспорт
	
	Возврат (ИсключенияПоКоду.Получить(ID) = Истина);

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПриСозданииОбъекта()

	ИсключенияПоID = Новый Соответствие();
	ИсключенияПоКоду = Новый Соответствие();

	ИмяФайлаПараметровПроверки = ПараметрыПриложения.ПолучитьКонтекстПараметров();
	Если НЕ ЗначениеЗаполнено(ИмяФайлаПараметровПроверки) Тогда
		Возврат;
	КонецЕсли;

	Лог = ПараметрыПриложения.Лог();
	Лог.Отладка("Чтение настроек проверки");

	ПараметрыПроверки = ФайловыеОперации.ПрочитатьОбъект(ИмяФайлаПараметровПроверки, Ложь);

	СчФильтровID = 0;
	СчФильтровКод = 0;
	Если ПараметрыПроверки.Свойство("diagnostics") Тогда

		ПараметрыПравил = ПараметрыПроверки.diagnostics;

		Если ПараметрыПравил.Свойство("filter_id") Тогда
			Для каждого id_проверки ИЗ ПараметрыПравил.filter_id Цикл
				ИсключенияПоID.Вставить(id_проверки, Истина);
				СчФильтровID = СчФильтровID + 1;
			КонецЦикла;
		КонецЕсли;

		Если ПараметрыПравил.Свойство("filter_code") Тогда
			Для каждого КодПроверки ИЗ ПараметрыПравил.filter_code Цикл
				ИсключенияПоКоду.Вставить(КодПроверки, Истина);
				СчФильтровКод = СчФильтровКод + 1;
			КонецЦикла;
		КонецЕсли;

	КонецЕсли;

	Если СчФильтровID > 0 ИЛИ СчФильтровКод > 0 Тогда
		
		Лог.Отладка("Подключены фильтры:");
		Если СчФильтровID > 0 Тогда
			Лог.Отладка("   по ID: %1", СчФильтровID);
		КонецЕсли;
		Если СчФильтровКод > 0 Тогда
			Лог.Отладка("   по коду: %1", СчФильтровКод);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
